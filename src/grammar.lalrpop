use crate::ast;
use crate::lexer::{LexicalError, Token, Location};
use either::Either::{self,Left,Right};

grammar<'input>(input: &'input str, hadron_name: &'input str);

// Top-level definitions

pub Hadron: ast::HadronDef = {
    <uses:UseDecl+>
    <defs:Definition+>  =>

    ast::HadronDef{id: hadron_name.to_owned(), use_decls: uses, defs: defs }

}

UseDecl: ast::UseDecl = {
    "use" <id: Identifier> <names:( "{" <Comma<SYMBOL>> "}" )?> =>
        ast::UseDecl{module_name: id, names: names}
}

pub Definition: ast::Definition = {
    <f:FlavorDef> => ast::Definition::F(f),
    <q:QuarkDef> => ast::Definition::Q(q),
    <b:BosonDef> => ast::Definition::B(b)
}

pub FlavorDef: ast::FlavorDef = {
    "flavor" <tps:TypeParamBlock?> <id:SYMBOL> <cs:ComposesBlock?> "is"
    <chs:ChannelDecl*>
    "end" ("@flavor")? =>
    ast::FlavorDef{id: id,
        type_params: tps,
        composes: cs,
        channels: chs}
}


pub QuarkDef: ast::QuarkDef = {
    "quark" <tps:TypeParamBlock?> <id:SYMBOL> <params:ValueParamBlock>
      <ps:ProvidesBlock>  "is"
      <channels:ChannelDecl*>
      <slots:SlotDecl*>
      <behaviors:BehaviorDecl*>
      "adopt" <beh:SYMBOL> "("<es:CommaOpt<Expr>>")"
      "end" ("@quark")?   =>
      ast::QuarkDef {
        id: id,
        type_params: tps,
        value_params: params,
        provides: ps,
        channels: channels,
        slots: slots,
        behaviors: behaviors,
        initial_beh: (beh.to_string(), es)
      }

}

pub BosonDef: ast::BosonDef = {
   "boson" <tps: TypeParamBlock?> <id:SYMBOL> "is"
      <options: Bar<BosonOptionDecl>>
   "end" ("@boson")? =>

   ast::BosonDef{id: id, type_params: tps, options: options}
}

BosonOptionDecl: ast::BosonOptionDecl = {
	<id:SYMBOL> <params:BosonParamsSpec> =>
    match params {
        ast::TempBosonParam::T(spec) => ast::BosonOptionDecl::TupleOptionDecl{id: id, value_types: spec},
        ast::TempBosonParam::S(spec) => ast::BosonOptionDecl::StructOptionDecl{id: id, field_types: spec}
    }
}

BosonParamsSpec: ast::TempBosonParam = {
    "("  <types:CommaOpt<Type>> ")" => ast::TempBosonParam::T(types),
    "{" <vs:Comma<ValueParamSpec>>"}" => ast::TempBosonParam::S(vs)
}



ChannelDecl: ast::ChannelDecl = {
    "chan" <id:SYMBOL> ":" <dir:Direction> <t:Type> =>
    ast::ChannelDecl{
        id: id,
        channel_type: ast::ChannelType{direction:dir, value_type: t}}
}

Direction: ast::Direction = {
    "In" => ast::Direction::In,
    "Out" => ast::Direction::Out,
    "Both" => ast::Direction::Both,
     => ast::Direction::Both
}

SlotDecl: ast::SlotDecl = {
    "slot" <id:SYMBOL> ":" <t:Type> "=" <e:Expr> =>

    ast::SlotDecl{ id: id, value_type: t, init_value: e}
}

ProvidesBlock: Vec<ast::Type> = {
    <("provides" <CommaOpt<Type>>)>,
    => Vec::new()

}

ComposesBlock: Vec<ast::Type> = {
    "("  <CommaOpt<Type>>  ")"
}

TypeParamBlock: Vec<ast::TypeParamSpec> = {
    "[" <Comma<TypeParamSpec>> "]"
}

TypeParamSpec: ast::TypeParamSpec = {
    <t:TYPEVAR> <constraint:(":" <Type>)?> =>

    ast::TypeParamSpec{type_var: ast::TypeVar{s:t}, constraint: constraint}
}

ValueParamBlock: Vec<ast::ValueParamSpec> = {
    "("  <pars:Comma<ValueParamSpec>?> ")" =>
    match pars {
        Some(params) => params,
        None => Vec::new()
    }
}

ValueParamSpec: ast::ValueParamSpec = {
    <id:SYMBOL> ":" <t:Type>  =>

    ast::ValueParamSpec{id: id, value_type: t}
}

BehaviorDecl: ast::BehaviorDecl = {
    "behavior" <id:SYMBOL> "(" <ps:CommaOpt<ValueParamSpec>> ")" "is"
        <body:Action+>
        "end" ("@behavior")? =>

        ast::BehaviorDecl{ id: id, params: ps, actions: body}
}


Identifier: ast::Identifier = {
 <mut syms:(<SYMBOL> "::")*> <s:SYMBOL> => {
                    syms.push(s);
                    syms
    }
}

// Actions

Action: Box<ast::Action> = {
    "par" "{" <acts:Action+> "}" => Box::new(ast::Action::Par(acts)),
    "seq" "{" <acts:Action+> "}" => Box::new(ast::Action::Seq(acts)),
    "select" "{" <acts:Action+> "}"  => Box::new(ast::Action::Select(acts)),
    "send" <ch:Expr> "(" <e:Expr> ")" => Box::new(ast::Action::Send{ target: ch, value: e}),
    <ReceiveAction>,
    <lval:SYMBOL> ":=" <e:Expr> => Box::new(ast::Action::Assign{target: lval, value: e}),
    "var" <s:SYMBOL> ":" <t:Type> "=" <e:Expr> => Box::new(ast::Action::VarDecl{
        id: s, var_type: t, init_value: e  }),
    <CondAction>,
    <WhileLoop>,
    <ForLoop>,
    "adopt" <b:SYMBOL> "(" <args:CommaOpt<Expr>> ")" => Box::new(ast::Action::AdoptAction{ id: b, arguments: args}),
    "exit" => Box::new(ast::Action::ExitAction)
}


ReceiveAction: Box<ast::Action> = {
    "rec"  <ch:Expr>  "do"
        <recs:ReceiveClause+>
        <else_acts:("else" <Action+>)?>
    "end" => Box::new(ast::Action::Recv{ source: ch, clauses: recs, else_clause: else_acts})
}

ReceiveClause: ast::ReceiveClause = {
    "on" <p:MessagePattern> "do" <acts:Action+> "end" => ast::ReceiveClause{pattern: p, actions: acts}
}

MessagePattern: ast::MessagePattern = {
    <i:Identifier> "(" <ss:Comma<SYMBOL>> ")" => ast::MessagePattern::TuplePattern(ss),
    <i:Identifier> "{" <ss:Comma<NameValue>> "}" => ast::MessagePattern::StructPattern(ss)
}

NameValue: (ast::Symbol, ast::Symbol) = {
    <n:SYMBOL> ":" <v:SYMBOL> => (n, v)
}

CondAction: Box<ast::Action> = {
    "cond" <cs:Bar<CondClause>>
    <else_acts:("else" <Action+>)?>
    "end" ( "@cond")? => Box::new(ast::Action::CondAction{
        clauses: cs, else_clause: else_acts
    })
}

CondClause: (ast::Expr, Vec<Box<ast::Action>>) = {
    <e: Expr> "then" <acts:Action+> "end" =>
        (e, acts)
}


WhileLoop: Box<ast::Action> = {
    "while" <e:Expr> "do" <acts:Action+> "end" ("@while")? =>
        Box::new(ast::Action::WhileAction{ cond: e, body: acts})
}

ForLoop: Box<ast::Action> = {
    "for" <idx:SYMBOL> ":" <range:Expr> "do" <acts:Action+> "end" ("@for")? =>
    Box::new(ast::Action::ForAction{ id: idx, range: range, body: acts})
}

//////////// Types

BoxedTypeArgsBlock: Vec<Box<ast::Type>> = {
    <("[" <Comma<Boxed<Type>>> "]")>
}


Type: ast::Type = {
    <tas:BoxedTypeArgsBlock?>  <id:Identifier> =>
        ast::Type::Named{ base_type_id: id,
                          type_arguments: tas},

    "chan" <d:Direction> <t:Type> => ast::Type::ChannelType(Box::new(t), d),
    <tv:TYPEVAR> => ast::Type::Var(ast::TypeVar{s:tv})
}


//////////// Expressions

BoxedExprList: Vec<Box<ast::Expr>> = {
    <Comma<Boxed<Expr>>>,
    => Vec::new()
 }


Expr: ast::Expr = {
    #[precedence(level="0")]
    "create" <t:Type> "(" <exprs:BoxedExprList> ")" => ast::Expr::CreateQuarkExpr{quark_type: t, args: exprs },
    <BosonValueExpr>,
    <s:STRLIT> => ast::Expr::LitStringExpr(s),
    <i:INTLIT> => ast::Expr::LitIntExpr(i),
    <f:FLOATLIT> => ast::Expr::LitFloatExpr(f),
    <c:CHARLIT> => ast::Expr::LitCharExpr(c),
    <SYMBOL> => ast::Expr::VariableExpr(<>),
    "(" <Expr> ")",
    #[precedence(level="1")] #[assoc(side="left")]
    "-" <e:Boxed<Expr>> => ast::Expr::OperatorExpr{ op: ast::Operator::OpMinus, args: vec![e] },
    "not" <e:Boxed<Expr>> => ast::Expr::OperatorExpr{ op: ast::Operator::OpNot, args: vec![e] },

    #[precedence(level="2")] #[assoc(side="left")]
    <owner:Boxed<Expr>>   <ch:("." <SYMBOL>)>  =>
                 ast::Expr::ChannelExpr(owner, ch),

    #[precedence(level="3")] #[assoc(side="left")]
    <l:Boxed<Expr>> <m:MultOp> <r:Boxed<Expr>> => ast::Expr::OperatorExpr{ op: m, args: vec![l, r] },
    #[precedence(level="4")] #[assoc(side="left")]
    <l:Boxed<Expr>> <m:AddOp> <r:Boxed<Expr>> => ast::Expr::OperatorExpr{ op: m, args: vec![l, r] },
    #[precedence(level="5")] #[assoc(side="left")]
    <l:Boxed<Expr>> <m:CompareOp> <r:Boxed<Expr>> => ast::Expr::OperatorExpr{ op: m, args: vec![l, r] },
    #[precedence(level="6")] #[assoc(side="left")]
    <l:Boxed<Expr>> <m:LogicOp> <r:Boxed<Expr>> => ast::Expr::OperatorExpr{ op: m, args: vec![l, r] }


}


MultOp: ast::Operator = {
     <"*"> => ast::Operator::OpTimes,
     <"/"> => ast::Operator::OpDiv,
     <"%"> => ast::Operator::OpMod
}

AddOp: ast::Operator = {
     <"+"> => ast::Operator::OpPlus,
     <"-"> => ast::Operator::OpMinus
}

CompareOp: ast::Operator = {
      <">"> => ast::Operator::OpGt,
      <"<"> => ast::Operator::OpLt,
      <">="> => ast::Operator::OpGe,
      <"<="> => ast::Operator::OpLe,
      <"=="> => ast::Operator::OpEq,
      <"!="> => ast::Operator::OpNotEq
}

LogicOp: ast::Operator = {
    <"and"> => ast::Operator::OpAnd,
    <"or"> => ast::Operator::OpOr
}

BosonValueExpr: ast::Expr = {
   "new" <type_args:BoxedTypeArgsBlock?> <boson_name:Identifier>
    <args:BosonArgs> => {
        match args {
            Left(a) => ast::Expr::BosonTupleExpr{tuple_type: boson_name,
                type_args: type_args,
                args: a},
            Right(b) => ast::Expr::BosonStructExpr{
                boson_type: boson_name,
                type_args: type_args,
                args: b
            }
        }
    }
}

BosonNameBinding: (ast::Symbol, Box<ast::Expr>) = {
    <id:SYMBOL> ":" <e:Boxed<Expr>> => (id, e)
}

BosonArgs: Either<Option<Vec<Box<ast::Expr>>>, Vec<(ast::Symbol, Box<ast::Expr>)>> = {
    "(" <args:Comma<Boxed<Expr>>?> ")" => Either::Left(args),
    "{" <args:Comma<BosonNameBinding>>  "}" => Either::Right(args)
}

LValue: ast::LValue = {
    <id:Identifier> => ast::LValue(id)
}

////////////// Macros


Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T> => {
                    v.push(e);
                    v
    }
};

CommaOpt<T>: Vec<T> = {
    Comma<T>,
    => Vec::new()
}

Boxed<T>: Box<T> = {
    <t:T> => Box::new(t)
}

Bar<T>: Vec<T> = {
    <mut v:(<T> "|")*> <e:T> => {
                    v.push(e);
                    v
    }
};

Dotted<T>: Vec<T> = {
    <mut v:(<T> ".")*> <e:T> => {
                    v.push(e);
                    v
    }
}



extern {
    type Location = Location;
    type Error = LexicalError;
    enum Token {
        "," => Token::Comma,
        "|" => Token::Bar,
        ":" => Token::Colon,
        "::" => Token::ColonColon,
        ":=" => Token::ColonEq,
        "/" => Token::Div,
        "." => Token::Dot,
        "=" => Token::Equal,
        "==" => Token::EqualEqual,
        "!=" => Token::NotEqual,
        ">=" => Token::GE,
        ">" => Token::GT,
        "<=" => Token::LE,
        "<" => Token::LT,
        "-" => Token::Minus,
        "%" => Token::Mod,
        "+" => Token::Plus,
        "*" => Token::Times,

        "(" => Token::LParen,
        ")"=> Token::RParen,
        "[" => Token::LBrack,
        "]" => Token::RBrack,
        "{" => Token::LCurly,
        "}" => Token::RCurly,
        "<<" => Token::LCH,
        ">>" => Token::RCH,

        "@behavior" => Token::AtBehavior,
        "@boson" => Token::AtBoson,
        "@cond" => Token::AtCond,
        "@flavor" => Token::AtFlavor,
        "@for" => Token::AtFor,
        "@quark" => Token::AtQuark,
        "@while" => Token::AtWhile,

        "and" => Token::And,
        "adopt" => Token::Adopt,
        "behavior" => Token::Behavior,
        "boson" => Token::Boson,
        "Both" => Token::Both,
        "chan" => Token::Chan,
        "cond" => Token::Cond,
        "create" => Token::Create,
        "do" => Token::Do,
        "end" => Token::End,
        "exit" => Token::Exit,
        "else" => Token::Else,
        "flavor" => Token::Flavor,
        "for" => Token::For,
        "hadron" => Token::Hadron,
        "In" => Token::In,
        "is" => Token::Is,
        "new" => Token::New,
        "not" => Token::Not,
        "on" => Token::On,
        "or" => Token::Or,
        "Out" => Token::Out,
        "par" => Token::Par,
        "provides" => Token::Provides,
        "quark" => Token::Quark,
        "rec" => Token::Rec,
        "select" => Token::Select,
        "send" => Token::Send,
        "seq" => Token::Seq,
        "slot" => Token::Slot,
        "then" => Token::Then,
        "use" => Token::Use,
        "var" => Token::Var,
        "while" => Token::While,

        SYMBOL => Token::Symbol(<ast::Symbol>),
        TYPEVAR => Token::TypeVar(<String>),
        STRLIT => Token::StrLit(<String>),
        INTLIT => Token::IntLit(<i64>),
        FLOATLIT => Token::FloatLit(<String>),
        CHARLIT => Token::CharLit(<char>),
    }
}














